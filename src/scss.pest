char = _{
    ASCII_ALPHANUMERIC | "_" | "-"
}
class = @{
    "." ~ (ASCII_ALPHA | "_") ~ char*
}
id = @{
    "#" ~ (ASCII_ALPHA | "_") ~ char*
}
identifier = @{
    (ASCII_ALPHA | "_") ~ char*
}
selector_part = {
    id | class | identifier
}
selector = {
    selector_part+
}
property = {identifier}
variable_name = {ASCII_ALPHA_LOWER ~ (ASCII_ALPHANUMERIC | "-")* }
string = ${ "\"" ~ inner ~ "\"" }
inner = @{ char_s* }
char_s = _{
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

// literal number
literal_unit_length = @{"px" | "in"}
literal_unit_period = @{"s"}
literal_unit = @{ literal_unit_length | literal_unit_period }
literal_number_minus_sign = @{"-"}
literal_number_integer = @{literal_number_minus_sign? ~ ASCII_DIGIT+}
literal_number_decimal_no_int = @{"." ~ ASCII_DIGIT+}
literal_number_decimal = ${ literal_number_minus_sign? ~ (ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT+ | literal_number_decimal_no_int) }
literal_number_scientific = ${ literal_number_decimal ~ "e" ~ literal_number_integer }
literal_number_no_unit = ${literal_number_decimal | literal_number_integer | literal_number_scientific }
literal_number_with_unit = ${literal_number_no_unit ~ literal_unit}
literal_number = ${ literal_number_with_unit | literal_number_no_unit}

WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
